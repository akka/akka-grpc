buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.gradle.publish:plugin-publish-plugin:0.9.10"
  }
}

plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.palantir.git-version' version '0.10.1'
    id 'com.gradle.plugin-publish' version '0.9.10'
}

group = "com.lightbend.akka.grpc"
// https://github.com/palantir/gradle-git-version/issues/97

def tag = "git describe --tags".execute().text.substring(1).split("-g")[0].replace("\n", "")
def finalVersion = (tag == versionDetails().lastTag.substring(1)) ? tag : tag.reverse().replaceFirst("-", "+").reverse() + "-" + versionDetails().gitHash.substring(0, 8)

version = finalVersion

gradlePlugin {
    plugins {
        akkaGrpcPlugin {
            id = 'com.lightbend.akka.grpc.gradle'
            implementationClass = 'akka.grpc.gradle.AkkaGrpcPlugin'
        }
    }
}

pluginBundle {
    website = 'https://developer.lightbend.com/docs/akka-grpc/current/'
    vcsUrl = 'https://github.com/akka/akka-grpc'
    description = 'Building streaming gRPC servers and clients on top of Akka Streams'
    tags = ['akka', 'streams', 'reactive']

    plugins {
        akkaGrpcPlugin {
            id = 'com.lightbend.akka.grpc.gradle'
            displayName = 'Akka gRPC'
        }
    }
}

jar {
    manifest {
        attributes 'Implementation-Version': project.version
    }
}

dependencies {
    compile localGroovy()
    compile gradleApi()

    implementation 'com.google.protobuf:protobuf-gradle-plugin:0.8.11'
}

repositories {
    mavenCentral()
}
