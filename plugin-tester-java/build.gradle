// Note: this setup is special to be able to run in CI using the akka-grpc build
//       for a regular gradle config for consumer projects look at docs and quickstarts
buildscript {
  ext {
    if (!System.getProperty('akka.grpc.project.version')) {
      throw new GradleException("System property `akka.grpc.project.version` is not provided. Use -Dakka.grpc.project.version=\$VERSION.")
    }
    akkaGrpcVersion = System.getProperty('akka.grpc.project.version')
  }
  repositories {
    mavenLocal()
    gradlePluginPortal()
    maven {
      url "https://repo.akka.io/maven"
    }
  }
}
plugins {
  id 'scala'
  id 'com.lightbend.akka.grpc.gradle' version "${akkaGrpcVersion}"
}

akkaGrpc {
  generateClient = true
  generateServer = true
  serverPowerApis = true // This isn't default
  includeStdTypes = true // This isn't default
  extraGenerators = [ ]
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url "https://repo.akka.io/maven"
  }
}

def scalaVersion = org.gradle.util.VersionNumber.parse(System.getenv("TRAVIS_SCALA_VERSION") ?: "2.13")
def scalaBinaryVersion = "${scalaVersion.major}.${scalaVersion.minor}"
def akkaHttpVersion = "10.5.0"
def akkaVersion = "2.9.0"

dependencies {
  implementation group: 'ch.megard', name: "akka-http-cors_${scalaBinaryVersion}", version: '1.1.3'
  testImplementation "com.typesafe.akka:akka-stream-testkit_${scalaBinaryVersion}:${akkaVersion}"
  testImplementation "com.typesafe.akka:akka-http-spray-json_${scalaBinaryVersion}:${akkaHttpVersion}"
  implementation "com.typesafe.akka:akka-pki_${scalaBinaryVersion}:${akkaVersion}"
  testImplementation "org.scalatest:scalatest_${scalaBinaryVersion}:3.2.12"
  testImplementation "org.scalatestplus:junit-4-12_${scalaBinaryVersion}:3.2.2.0"
}
